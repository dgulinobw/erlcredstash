%%% @author Drew Gulino <dgulino@bandwidth.com>
%%% @doc
%%% Erlang implementation of credstash (https://github.com/fugue/credstash)
%%% @end
-module(credstash).

-behaviour(application).
-include_lib("eunit/include/eunit.hrl").

-export([start/2, stop/1]).

-export([
        new/2, new/4, new/6, 
        delete_secret/2, delete_secret/3,
        get_all_secrets/1, get_all_secrets/2,
        get_secret/2, get_secret/3 ,get_secret/4,
        list_secrets/1, list_secrets/2,
        put_secret/3, put_secret/4, put_secret/5,
        get_env/0
       ]).

-define(DEFAULT_DDB_TABLE, <<"credential-store">>).
-define(DEFAULT_INITIAL_VERSION, "0000000000000000001").
-define(KMS_KEY, <<"alias/credstash">>).

start(_Type,_Args) ->
  ok.

stop(_State) ->
    ok.


%%%------------------------------------------------------------------------------
%%% Library initilization
%%%------------------------------------------------------------------------------

default_config() ->
  { erlcloud_aws:default_config(),
    erlcloud_aws:default_config()
  }.

new(AccessKeyID, SecretAccessKey) ->
  KmsConfig = erlcloud_kms:new(AccessKeyID, SecretAccessKey),
  DdbConfig = erlcloud_ddb2:new(AccessKeyID, SecretAccessKey),
  {KmsConfig, DdbConfig}.

new(AccessKeyID, SecretAccessKey, KmsHost, DdbHost) ->
  KmsConfig = erlcloud_kms:new(AccessKeyID, SecretAccessKey, KmsHost),
  DdbConfig = erlcloud_ddb2:new(AccessKeyID, SecretAccessKey, DdbHost),
  {KmsConfig, DdbConfig}.

new(AccessKeyID, SecretAccessKey, KmsHost, DdbHost, KmsPort, DdbPort) ->
  KmsConfig = erlcloud_kms:new(AccessKeyID, SecretAccessKey, KmsHost, KmsPort),
  DdbConfig = erlcloud_ddb2:new(AccessKeyID, SecretAccessKey, DdbHost, DdbPort),
  {KmsConfig, DdbConfig}.

get_env() ->
    default_config().

%%%------------------------------------------------------------------------------
%%% delete_secret
%%%------------------------------------------------------------------------------

%%------------------------------------------------------------------------------
%% @doc 
%%
%% ===Example===
%% `
%% credstash:delete_secret(<<"test">>,<<"credential-store">>).
%% '
%% @end
%%------------------------------------------------------------------------------
delete_secret(Name, Table, Config) ->
  {_KmsConfig, DdbConfig} = case Config of
    env -> default_config();
    _ -> Config
  end,           
  {ok, DdbResponse } = erlcloud_ddb2:scan(Table,
                                       [
                                       {filter_expression, <<"#N = :name">>}, 
                                       {expression_attribute_values, [{<<":name">>, Name}]}, 
                                       {projection_expression, <<"#N, version">>},
                                       {expression_attribute_names, [{<<"#N">>, <<"name">>}]}
                                       ],
                                       DdbConfig
                                          ),
  Secrets = DdbResponse,
  DeleteResponse = lists:map(fun(Secret) -> erlcloud_ddb2:delete_item(Table, 
                                             [{<<"name">>,{s, proplists:get_value(<<"name">>,Secret)}},
                                              {<<"version">>,{s, proplists:get_value(<<"version">>,Secret)}}] , 
                                             [{return_values, all_old}], DdbConfig) 
                                            end, Secrets ),
  DeleteResponse.

delete_secret(Name, Config) ->
  Table = ?DEFAULT_DDB_TABLE,
  delete_secret(Name, Table, Config).

%%%------------------------------------------------------------------------------
%%% get_all_secrets
%%%------------------------------------------------------------------------------

%%------------------------------------------------------------------------------
%% @doc 
%%
%% ===Example===
%% `
%% credstash:get_all_secrets(<<"credential-store">>, env).
%% '
%% @end
%%------------------------------------------------------------------------------
get_all_secrets(Table, Config) ->
  {ok,Secrets} = list_secrets(Table, Config),
  Values = lists:map(fun(Secret) -> 
                         Name = proplists:get_value(<<"name">>,Secret),
                         {ok, Value} = get_secret(Name,Table, Config),
                         {Name, Value}
                     end, Secrets),
  {ok, Values}.

get_all_secrets(Config) ->
  Table = ?DEFAULT_DDB_TABLE,
  get_all_secrets(Table, Config).

%%%------------------------------------------------------------------------------
%%% get_secret
%%%------------------------------------------------------------------------------

%%------------------------------------------------------------------------------
%% @doc 
%%
%% ===Example===
%% `
%% credstash:get_secret(<<"test">>,<<"credential-store">>,<<"0000000000000000001">>, env).
%% '
%% @end
%%------------------------------------------------------------------------------
get_secret(Name, Table, Version, Config) ->
  {_KmsConfig, DdbConfig} = case Config of
    env -> default_config();
    _ -> Config
  end,           
  {ok, Ciphertext } = erlcloud_ddb2:get_item(Table, [{<<"name">>,{s,Name}},{<<"version">>,{s, Version}}], DdbConfig),
  decrypt_secret(Ciphertext, Config).

get_secret(Name, Table, Config) ->
  {_KmsConfig, DdbConfig} = case Config of
    env -> default_config();
    _ -> Config
  end,           
  {ok, VersionResponse} = erlcloud_ddb2:q(Table,
                                           <<"#N = :name">>,
                                          [
                                           {expression_attribute_values,
                                              [ {<<":name">>, Name} ]},
                                           {limit, 1},
                                           {consistent_read, true},
                                           {scan_index_forward, false},
                                           {expression_attribute_names, [{<<"#N">>, <<"name">>}]}
                                          ],
                                          DdbConfig
                                        ),
  case VersionResponse of
    [] -> {notfound, []};
    _ -> 
    {Count,_} = string:to_integer(proplists:get_value(<<"Count">>,VersionResponse)),
    case Count == 0 of
      true -> {notfound, []};
      false ->
        decrypt_secret(hd(VersionResponse), Config)
    end
  end.

get_secret(Name, Config) ->
  Table = ?DEFAULT_DDB_TABLE,
  get_secret(Name, Table, Config).

%%%------------------------------------------------------------------------------
%%% list_secrets
%%%------------------------------------------------------------------------------

%%------------------------------------------------------------------------------
%% @doc 
%%
%% ===Example===
%% `
%% credstash:list_secrets(<<"credential-store">>, env).
%% '
%% @end
%%------------------------------------------------------------------------------
list_secrets(Table, Config) ->
  {_KmsConfig, DdbConfig} = case Config of
    env -> default_config();
    _ -> Config
  end,           
  DdbResponse = erlcloud_ddb2:scan(Table,
                                       [
                                       {projection_expression, <<"#N, version">>},
                                       {expression_attribute_names, [{<<"#N">>, <<"name">>}]}
                                       ], 
                                       DdbConfig
                                          ),
  DdbResponse. 

list_secrets(Config) ->
  Table = ?DEFAULT_DDB_TABLE,
  list_secrets(Table, Config).


encrypt_secret(Name, Secret, Version, Config) ->
  {KmsConfig, _DdbConfig} = case Config of
                             env -> default_config();
                             _ -> Config
                           end,
  KmsKey = ?KMS_KEY,
  NumberOfBytes=64,
  {ok, KmsResponse} = erlcloud_kms:generate_data_key(KmsKey, [{number_of_bytes, NumberOfBytes},{encryption_context, [{}] } ], KmsConfig),
  CiphertextBlob = proplists:get_value(<<"CiphertextBlob">>,KmsResponse),
  Plaintext = base64:decode(proplists:get_value(<<"Plaintext">>,KmsResponse)),
  DataKey=binary:part(Plaintext, 0, 32),
  HmacKey=binary:part(Plaintext, 32, byte_size(Plaintext) - byte_size(DataKey)),
  WrappedKey = CiphertextBlob,
  Ivec = <<1:128>>,
  State = crypto:stream_init(aes_ctr, DataKey, Ivec),
  {_NewState, CText} = crypto:stream_encrypt(State, Secret),
  Hmac = crypto:hmac(sha256, HmacKey, CText),
  B64Hmac = hexlify(Hmac),
  Data = [{<<"name">>, Name},
    {<<"version">>, Version},
    {<<"key">>, WrappedKey},
    {<<"contents">>, base64:encode(CText)},
    {<<"hmac">>, B64Hmac}],
  Data.


%%%------------------------------------------------------------------------------
%%% put_secret
%%%------------------------------------------------------------------------------

%%------------------------------------------------------------------------------
%% @doc 
%%
%% ===Example===
%% `
%% credstash:put_secret(<<"test">>,<<"best">>,<<"credential-store">>,<<"0000000000000000001">>, env).
%% '
%% @end
%%------------------------------------------------------------------------------
put_secret(Name, Secret, Table, Version, Config) ->
  {_KmsConfig, DdbConfig} = case Config of
                             env -> default_config();
                             _ -> Config
                           end,
  Data = encrypt_secret(Name, Secret, Version, Config),
  DdbResponse = erlcloud_ddb2:put_item(Table, Data, [], DdbConfig),
  DdbResponse.

put_secret(Name, Secret, Table, Config) ->
  Version = ?DEFAULT_INITIAL_VERSION,
  put_secret(Name, Secret, Table, Version, Config).

put_secret(Name, Secret, Config) ->
  Table = ?DEFAULT_DDB_TABLE,
  put_secret(Name, Secret, Table, Config).

decrypt_secret(Ciphertext, Config) ->
  {KmsConfig, _DdbConfig} = case Config of
    env -> default_config();
    _ -> Config
  end,           
  KeyBase64 = proplists:get_value(<<"key">>,Ciphertext),
  Hmac = proplists:get_value(<<"hmac">>,Ciphertext),
  Contents = proplists:get_value(<<"contents">>,Ciphertext),
  {ok, KMS_Response} = erlcloud_kms:decrypt(KeyBase64, [], KmsConfig), 
  Plaintext = base64:decode(proplists:get_value(<<"Plaintext">>,KMS_Response)),
  Key=binary:part(Plaintext, 0, 32),
  HmacKey=binary:part(Plaintext, 32, byte_size(Plaintext) - byte_size(Key)),
  DecodedContents = base64:decode(Contents),
  Digest = crypto:hmac(sha256, HmacKey, DecodedContents ),
  HexDigest = hexlify(Digest),
  case Hmac == HexDigest of
    false ->
      {error, io_lib:format("Computed HMAC does not match stored HMAC", [])};
    true ->
      Ivec = <<1:128>>,
      State = crypto:stream_init(aes_ctr, Key, Ivec),
      {_NewState, Text} = crypto:stream_decrypt(State, DecodedContents),
      {ok, Text}
  end.


my_test_() ->
  {setup,
    fun() ->
      {ok, _AppsList} = application:ensure_all_started(erlcredstash)
    end,
    fun({ok, AppList}) ->
      lists:foreach(fun (App) -> quiet_stop(App) end, AppList)
    end,
    [
      ?_test(decrypt_secret_t())
    ]}.

decrypt_secret_t() ->
  ssl:start(),
  erlcloud:start(),
  Name = <<"Test">>,
  Secret = <<"1234567890abcdef">>,
  Version = <<"1.0">>,
  Config = default_config(),
  Encrypted = encrypt_secret(Name, Secret, Version, Config),
  {ok, Decrypted} = decrypt_secret(Encrypted, Config),
  ?assertEqual(Secret, Decrypted).

quiet_stop(App) ->
  error_logger:tty(false),
  Res = application:stop(App),
  error_logger:tty(true),
  Res.

hexlify(Bin) when is_binary(Bin) ->
    << <<(hex(H)),(hex(L))>> || <<H:4,L:4>> <= Bin >>.

hex(C) when C < 10 -> $0 + C;
hex(C) -> $a + C - 10.
